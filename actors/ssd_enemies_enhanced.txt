//===========================================================================
// SSD Enhanced Enemies
//===========================================================================

ACTOR SSDEnhancedImp : DoomImp 30015
{
	SpawnID 137
	Health 220
	Mass 120
	ReactionTime 5
	PainChance 133
	Radius 24
	Height 67
	Scale 1.2
	SeeSound centor_a
	DeathSound ogre_d
	MeleeSound e4aln_s
	HitObituary "%o was slashed by an enhanced imp."
	Obituary "%o was burned by an enhanced imp."
	MeleeDamage 4
	States
	{
	Spawn:
		TROO A 1 A_Look
		TROO A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		TROO AB 10 A_Look
		Goto Spawn+2
	See:
		TROO AABBCCDD 2 A_Chase
		Loop
	Melee:
		TROO EF 5 A_FaceTarget
		TROO G 5 A_MeleeAttack
		Goto See
	Missile:
		TROO EF 5 A_FaceTarget
		TROO G 0 A_PlaySound ("imp/attack")
		TROO G 5 A_CustomMissile ("ReiverFireball", 40, 0, 0)  
		Goto See
	Pain:
		TROO H 2
		TROO H 2 A_Pain
		Goto See
	Death:
		TROO I 8 ACS_ExecuteAlways (200, 0, 7, 0, 0)
		TROO J 8 A_Scream
		TROO K 6
		TROO L 6 A_Fall
		TROO M -1   
		Stop
	XDeath:
		TROO N 5 ACS_ExecuteAlways (200, 0, 7, 0, 0)
		TROO O 5 A_XScream
		TROO P 5
		TROO Q 5 A_Fall
		TROO RST 5
		TROO U -1   
		Stop
	Raise:
		TROO M 8
		TROO KLJI 8
		Goto See
	}
}

ACTOR SSDSuicidalSoul : LostSoul 30016
{
	SpawnID 149
	Health 200
	Mass 60
	Damage 4
	ExplosionDamage 90
	ExplosionRadius 128
	PainChance 256
	ReactionTime 5
	+NOBLOOD
	AttackSound fyrimp_f
	DeathSound xplosion
	Obituary "%o was blown up by a suicidal soul."
	States
	{
	Spawn:
		SKUL A 1 BRIGHT A_Look
		SKUL A 0 BRIGHT ACS_ExecuteAlways (232, 0, 0, 0, 0)
		SKUL AB 10 BRIGHT A_Look
		Goto Spawn+2
	See:
		SKUL AB 4 BRIGHT A_Chase
		Loop
	Melee:	
		SKUL F 0 BRIGHT A_PlaySound ("weapons/rocklx") 
		SKUL F 0 BRIGHT A_SetInvulnerable 
		SKUL F 0 BRIGHT A_Explode 
		SKUL F 0 BRIGHT A_UnsetInvulnerable 
		SKUL F 1 BRIGHT A_Die 
		Goto Death
	Missile:
		SKUL C 7 BRIGHT A_FaceTarget
		SKUL D 4 BRIGHT A_SkullAttack
		SKUL CD 1 BRIGHT
		Goto See
	Death:
		SKUL F 0 BRIGHT A_NoGravity
		SKUL F 6 BRIGHT ACS_ExecuteAlways (200, 0, 9, 0, 0)
		SKUL G 6 BRIGHT A_Scream
		SKUL H 6 BRIGHT
		SKUL I 6 BRIGHT A_Fall
		SKUL J 6
		SKUL K 6    
		Stop
	}
}

ACTOR SSDEnhancedDemon : Demon 30017
{
	SpawnID 138
	Health 500
	Speed 12
	ReactionTime 2
	PainChance 120
	Mass 480
	Radius 36
	Height 67
	SeeSound icebst_a
	AttackSound fiend_f
	PainSound fiend_p
	DeathSound fiend_d
	ActiveSound fiend_r
	Obituary "%o was eaten by an enhanced demon."
	Scale 1.2
	States
	{
	Spawn:
		SARG A 1 A_Look
		SARG A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		SARG AB 10 A_Look
		Goto Spawn+2
	See:
		SARG ABCD 1 A_Chase
		Loop
	Melee:
		SARG EF 4 A_FaceTarget
		SARG G 4 A_SargAttack
		Goto See
	Pain:
		SARG H 1
		SARG H 1 A_Pain
		Goto See
	Death:
		SARG I 8 ACS_ExecuteAlways (200, 0, 13, 0, 0)
		SARG J 8 A_Scream
		SARG K 4
		SARG L 4 A_Fall
		SARG M 4
		SARG N -1    
		Stop
	}
}

ACTOR SSDRailElemental : PainElemental 30018
{
	SpawnID 150
	Health 400
	Speed 10
	Damage 100
	BloodColor Blue
	ReactionTime 6
	SeeSound korax_a
	PainSound heresi_p
	DeathSound heresi_d
	ActiveSound heresi_d
	Obituary "%o was shafted by a rail elemental."
	States
	{
	Spawn:
		PAIN A 1 A_Look
		PAIN A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		PAIN A 10 A_Look
		Goto Spawn+2
	See:
		PAIN AABBCC 3 A_Chase
		Loop
	Missile:
		PAIN D 7 BRIGHT A_FaceTarget
		PAIN E 7 BRIGHT A_FaceTarget
		PAIN F 3 BRIGHT A_FaceTarget
		PAIN F 4 BRIGHT A_MonsterRail
		PAIN E 7
		PAIN D 7
		Goto See
	Pain:
		PAIN G 6
		PAIN G 6 A_Pain
		Goto See
	Death:
		PAIN H 8 ACS_ExecuteAlways (200, 0, 17, 0, 0)
		PAIN I 8 A_Scream
		PAIN JK 8
		PAIN L 8 
		PAIN M 8
		Stop
	}
}

ACTOR SSDBaronOfHell : BaronOfHell replaces BaronOfHell 30019
{
	SpawnID 3
	ReactionTime 5
	MeleeSound "baron/melee"
	MeleeDamage 10
	BloodColor DarkOliveGreen
	States
	{
	Spawn:
		BOSS A 1 A_Look
		BOSS A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		BOSS AB 10 A_Look
		Goto Spawn+2
	See:
		BOSS AABBCCDD 2 A_Chase
		Loop
	Melee:
		BOSS E 8 A_FaceTarget
		BOSS F 8 A_FaceTarget
		BOSS G 8 A_MeleeAttack
		Goto See
	Missile:
		BOSS E 0 A_FaceTarget
		BOSS E 8
		BOSS E 8 A_CustomMissile ("BaronBall", 64, 20, 0)
		BOSS F 8 A_CustomMissile ("BaronBall", 48, 20, 0)
		BOSS G 8 A_CustomMissile ("BaronBall", 32, 10, 0)
		Goto See
	Death:
		BOSS I  8 ACS_ExecuteAlways (200, 0, 20, 0, 0)
		BOSS J  8 A_Scream
		BOSS K  8
		BOSS L  8 A_Fall
		BOSS MN 8
		BOSS O -1 
		Stop
	}
}

ACTOR SSDEnhancedCacodemon : Cacodemon 30020
{
	SpawnID 148
	Health 1200
	ReactionTime 5
	PainChance 96
	Mass 600
	Radius 47
	Height 84
	Scale 1.5
	BloodColor Blue
	SeeSound octa_a
	DeathSound octa_d
	Obituary "%o was smitten by an enhanced cacodemon."
	HitObituary "%o got too close to an enhanced cacodemon."
	MeleeDamage 8
	States
	{
	Spawn:
		HEAD A 1 A_Look
		HEAD A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		HEAD A 10 A_Look
		Goto Spawn+2
	See:
		HEAD A 2 A_Chase
		Loop
	Melee:
		HEAD B 5 A_FaceTarget
		HEAD C 5 A_FaceTarget
		HEAD D 5 A_MeleeAttack
		Goto See
	Missile:
		HEAD B 4 A_FaceTarget
		HEAD C 4 A_FaceTarget
		HEAD D 0 A_CustomMissile ("CacodemonBall", 32, 0, 0)
		HEAD D 0 A_CustomMissile ("CacodemonBall", 32, 0, -10)
		HEAD D 4 A_CustomMissile ("CacodemonBall", 32, 0, 10)
		Goto See
	Death:
		HEAD G 8 ACS_ExecuteAlways (200, 0, 24, 0, 0)
		HEAD H 8 A_Scream
		HEAD I 8
		HEAD J 8   
		HEAD K 8 A_Fall
		HEAD L -1 A_SetFloorClip
		Stop
	}
}

ACTOR SSDRevenantTerminator : Revenant 30021
{
	SpawnID 25
	Health 800
	Mass 600
	PainChance 100
	Radius 24
	Height 96
	Scale 1.2
	SeeSound e4aln_a
	PainSound e4aln_p
	DeathSound korax_d
	ActiveSound e4aln_r
	MeleeSound "skeleton/melee"
	HitObituary "%o was punched by a revenant terminator."
	Obituary "%o was terminated by a revenant terminator's rocket."
	States
	{
	Spawn:
		SKEL A 1 A_Look
		SKEL A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		SKEL AB 10 A_Look
		Goto Spawn+2
	Melee:
		SKEL G 1 A_FaceTarget
		SKEL G 6 A_SkelWhoosh
		SKEL H 6 A_FaceTarget
		SKEL I 6 A_SkelFist
		Goto See
	Missile:
		SKEL J 1 A_FaceTarget 
		SKEL J 9 A_FaceTarget
		SKEL K 10 A_CustomMissile ("Rocket", 64, 0, 0)
		SKEL K 10 A_FaceTarget
		Goto See
	Death:
		SKEL L 7 ACS_ExecuteAlways (200, 0, 28, 0, 0)
		SKEL M 7
		SKEL N 7 A_Scream
		SKEL O 7 A_Fall
		SKEL P 7
		SKEL Q -1
		Stop
	}
}

ACTOR SSDShadowArchvile : Archvile 30022
{
	SpawnID 69
	Health 1500
	Mass 700
	Scale 1.4
	Radius 24
	Height 101
	RenderStyle Fuzzy
	SeeSound reiver_a
	PainSound reiver_p
	DeathSound reiver_d
	ActiveSound reiver_r
	Obituary "%o was spookily incinerated by a shadow archvile."
	States
	{
	Spawn:
		VILE A 1 A_Look
		VILE A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		VILE AB 10 A_Look
		Goto Spawn+2
	Missile:
		VILE G 1 BRIGHT A_VileStart
		VILE G 6 BRIGHT A_FaceTarget 
		VILE H 5 BRIGHT A_VileTarget
		VILE IJKLMN 5 BRIGHT A_FaceTarget 
		VILE O 5 BRIGHT A_VileAttack
		VILE P 13
		Goto See
	Death:
		VILE Q 7 ACS_ExecuteAlways (200, 0, 40, 0, 0)
		VILE R 7 A_Scream
		VILE S 7 A_Fall
		VILE TUVWXY 7
		VILE Z -1
		Stop
	}
}

ACTOR SSDGiantMancubus : Fatso 30023
{
	SpawnID 24
	Health 2500
	Mass 2000
	PainChance 0
	Radius 96
	Height 128
	Scale 2.0
	+NORADIUSDMG
	SeeSound ogre_a
	PainSound korax_p
	DeathSound boss2_d
	ActiveSound boss2m_r
	Obituary "%o was flattened by a giant mancubus."
	States
	{
	Spawn:
		FATT A 1 A_Look
		FATT A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		FATT AB 15 A_Look
		Goto Spawn+2
	Missile:
		FATT G 20 A_FatRaise
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -30)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -20)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -10)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 0)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 10)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 20)
		FATT H 10 A_CustomMissile ("FatShot", 32, 0, 30)
		FATT IG 5
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -45)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -30)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -15)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 0)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 15)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 30)
		FATT H 10 A_CustomMissile ("FatShot", 32, 0, 45)
		FATT IG 5
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -15)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -10)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, -5)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 0)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 5)
		FATT H 0 A_CustomMissile ("FatShot", 32, 0, 10)
		FATT H 10 A_CustomMissile ("FatShot", 32, 0, 5)
		FATT IG 5
		Goto See
    	Death:
		FATT K 6 ACS_ExecuteAlways (200, 0, 60, 0, 0)
		FATT L 6 A_Scream
		FATT M 6 A_Fall
		FATT NOPQRS 6
		FATT T -1   
	     	Stop
	}
}

ACTOR SSDSpiderMastermind : SpiderMastermind replaces SpiderMastermind 30024
{
	SpawnID 7
	PainChance 0
	States
	{
	Spawn:
		SPID A 1 A_Look
		SPID A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		SPID AB 10 A_Look
		Goto Spawn+2
	Death:
		SPID J 1 ACS_ExecuteAlways (200, 0, 80, 0, 0)
		SPID J 19 A_Scream
		SPID K 10 A_Fall
		SPID LMNOPQR 10
		SPID S 30
		SPID S -1    
		Stop
	}
}

ACTOR SSDCyberdemon : Cyberdemon replaces Cyberdemon 30025
{
	SpawnID 114
	PainChance 0
	States
	{
	Spawn:
		CYBR A 1 A_Look
		CYBR A 0 ACS_ExecuteAlways (232, 0, 0, 0, 0)
		CYBR AB 10 A_Look
		Goto Spawn+2
	Death:
		CYBR H 10 ACS_ExecuteAlways (200, 0, 100, 0, 0)
		CYBR I 10 A_Scream
		CYBR JKL 10
		CYBR M 10 A_Fall
		CYBR NO 10
		CYBR P 30
		CYBR P -1
		Stop
	}
}