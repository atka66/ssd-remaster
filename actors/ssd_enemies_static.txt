//===========================================================================
// SSD Static Enemies and Hazards
//===========================================================================

ACTOR SSDFire 20071
{
	SpawnID 147
	Speed 0
	Radius 30
	Height 72
	Mass 400
	MeleeDamage 1
	+NOGRAVITY
	+NONSHOOTABLE
	+INVULNERABLE
	+NOTARGET
	+CANPASS
	HitObituary "%o was fired."
	States
	{
	Spawn:
		FYRE ABCDEFGHIJKLMN 2 BRIGHT A_Look
		Loop
	See:
		FYRE ABCDEFGHIJKLMN 2 BRIGHT A_Chase
		Loop
	Melee:
		FYRE A 2 BRIGHT A_MeleeAttack
		FYRE BC 2 BRIGHT 
		FYRE D 2 BRIGHT A_MeleeAttack
		FYRE EF 2 BRIGHT 
		FYRE G 2 BRIGHT A_MeleeAttack
		FYRE HI 2 BRIGHT 
		FYRE J 2 BRIGHT A_MeleeAttack
		FYRE KL 2 BRIGHT 
		FYRE M 2 BRIGHT A_MeleeAttack
		FYRE N 2 BRIGHT 
		Goto See
	}
}

ACTOR SSDBarrelNoGravity : ExplosiveBarrel 20075

{
	SpawnID 188
	+NOGRAVITY
	+DONTFALL
}

ACTOR SSDMine 20077
{
	SpawnID 135
	Health 20
	Speed 0
	Radius 16
	Height 16
	Mass 100
	DeathHeight 16
	+LOOKALLAROUND
	+SHOOTABLE
	+CANPASS
	+DROPOFF
	+NOBLOOD
	HitObituary "%o trod on a mine."
	States
	{
	Spawn:
		MINE A 2 A_Look
		Loop
	See:
		MINE A 2 A_Chase
		Loop
	Melee:
		MINE A 0 A_Die
		Goto Death
	Death:
		MINE A 0 A_PlaySound ("weapons/rocklx")
		MINE A 8 A_Explode
		MISL BC 6
		MISL D 4
		Stop
	}
}

ACTOR SSDTurret 20091
{
	Health 400
	Speed 0
	Radius 16
	Height 16
	Mass 9999
	+LOOKALLAROUND
	+NORADIUSDMG
	+SPAWNCEILING
	+NOGRAVITY
	+SHOOTABLE
	+NOBLOOD
      +SOLID
	+DONTFALL
	SeeSound siren1
	DeathSound explosion3
	HitObituary "%o was tracked by a turret."
	States
	{
	Spawn:
		TURT A 2 A_Look
		Loop
	See:
		TURT A 2 A_Chase
		Loop
	Missile:
		TURT B 2 A_FaceTarget
		TURT D 0 A_PlaySound ("turret4")
		TURT D 2 A_CustomMissile ("Spectre", 0, 0, 0)
		TURT A 100
		Goto Missile+1
	Death:
		BOOM ABCD 2
		TURT C -1
		Stop
	}
}

ACTOR SSDCrusher 20094
{
	Radius 1
	Height 192
	+NOGRAVITY
	+NOCLIP
	States
	{
	Spawn:
		CRSH A -1
	}
}